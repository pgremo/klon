%header%

GRAMMARTYPE = "LL"

AUTHOR = "Patrick Gremo"

%tokens%

NUMBER              = <<((-?[0-9]+)|(-?[0-9]*\.[0-9]+))(e-?[0-9]+)?|0[xX][0-9a-fA-F]+>>
OPERATOR            = <<[:='~!@$%\^&*\-+\\<>|?/.]+>>
IDENTIFIER          = <<[a-zA-Z][a-zA-Z0-9_.]*>>
STRING              = <<("[^"]*")>>
TERMINATOR          = <<[\r\n;]>>
SET                 = ":="
UPDATE              = "="
COMMA               = ","
LPAREN              = "("
RPAREN              = ")"
LBRACK              = "["
RBRACK              = "]"
LBRACE              = "{"
RBRACE              = "}"
WHITE_SPACE         = <<[ \t]+>> %ignore%
POUND_COMMENT       = <<#.*>> %ignore%
SLASH_COMMENT       = <<//.*>> %ignore%
MULTI_COMMENT       = <</\*([^*]|\*[^/])*\*/>> %ignore%


%productions%

MessageChain 	= ( Message | TERMINATOR )+ ;        

Message = StandardMessage
        | SlotOperation ;
        
StandardMessage = ( Group | Symbol Group? ) Attached? ;

SlotOperation = IDENTIFIER ( SET | UPDATE ) Attached ;

Group = "(" MessageChain { "," MessageChain } ")"
      | "{" MessageChain { "," MessageChain } "}"
      | "[" MessageChain { "," MessageChain } "]" ;

Attached = Message;
        
Symbol = IDENTIFIER
       | OPERATOR
       | STRING
       | NUMBER ;      
