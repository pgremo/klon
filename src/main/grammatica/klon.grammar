%header%

GRAMMARTYPE = "LL"

AUTHOR = "Patrick Gremo"

%tokens%

OPERATOR            = <<([:='~!@$%\^&*\-+\\<>|?/.]+)|(return|break|continue)>>
IDENTIFIER          = <<[a-zA-Z][a-zA-Z0-9_]*>>
STRING              = <<("[^"]*")>>
NUMBER              = <<(-?[0-9]+)|(-?[0-9]*\.[0-9]+(e-?[0-9]+)?)>>
TERMINATOR          = <<[\r\n;]>>
SET                 = ":="
UPDATE              = "="
COMMA               = ","
LPAREN              = "("
RPAREN              = ")"
LBRACK              = "["
RBRACK              = "]"
LBRACE              = "{"
RBRACE              = "}"
WHITE_SPACE         = <<[ \t]+>> %ignore%
POUND_COMMENT       = <<#.*>> %ignore%
SLASH_COMMENT       = <<//.*>> %ignore%
MULTI_COMMENT       = <</\*([^*]|\*[^/])*\*/>> %ignore%


%productions%

MessageChain 	= ( Message | TERMINATOR )+ ;        

Message = StandardMessage
        | SlotSet
        | SlotUpdate ;
        
StandardMessage = ( Arguments | Symbol Arguments? ) Attached? ;

SlotSet = IDENTIFIER ":=" Attached ;

SlotUpdate = IDENTIFIER "=" Attached ;

Arguments = "(" MessageChain { "," MessageChain } ")"
          | "{" MessageChain { "," MessageChain } "}"
          | "[" MessageChain { "," MessageChain } "]" ;

Attached = Message;
        
Symbol = IDENTIFIER
       | OPERATOR
       | STRING
       | NUMBER ;
       
